def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def flutterRoot = localProperties.getProperty('flutter.sdk')
if (flutterRoot == null) {
    throw new GradleException("Flutter SDK not found. Define location with flutter.sdk in the local.properties file.")
}

def flutterVersionCode = localProperties.getProperty('flutter.versionCode')
if (flutterVersionCode == null) {
    flutterVersionCode = '1'
}

def flutterVersionName = localProperties.getProperty('flutter.versionName')
if (flutterVersionName == null) {
    flutterVersionName = '1.0'
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'com.google.gms.google-services'
apply from: "$flutterRoot/packages/flutter_tools/gradle/flutter.gradle"
apply plugin: 'com.onesignal.androidsdk.onesignal-gradle-plugin'
apply from: project(':flutter_config').projectDir.getPath() + "/dotenv.gradle"

project.ext.envConfigFiles = [
        devdebug   : ".env",
        devrelease : ".env",
        proddebug  : ".env",
        prodrelease: ".env",
]

android {
    compileSdkVersion 31
    ndkVersion "21.4.7075529"

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    defaultConfig {
        applicationId "cz.loono.app"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode flutterVersionCode.toInteger()
        versionName flutterVersionName

//        "added due to: The number of method references in a .dex file cannot exceed 64k"
        multiDexEnabled true
        ndk {
            debugSymbolLevel 'SYMBOL_TABLE'
        }
    }

    signingConfigs {
        release {
            if (System.getenv()["CI"]) { // CI=true is exported by Codemagic
                storeFile file(System.getenv()["FCI_KEYSTORE_PATH"])
                storePassword System.getenv()["FCI_KEYSTORE_PASSWORD"]
                keyAlias System.getenv()["FCI_KEY_ALIAS"]
                keyPassword System.getenv()["FCI_KEY_PASSWORD"]
            } else {
                storeFile file("/path/to/prod/or/dev/keystore.jks")
                storePassword "prod-or-dev-password"
                keyAlias "upload"
                keyPassword "prod-or-dev-password"
            }
        }
        debug {
            storeFile = file("loono-debug")
            storePassword = "Vd3ryhCWsy3PJHP"
            keyAlias = "vcas-debug"
            keyPassword = "Vd3ryhCWsy3PJHP"
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
        }
        debug {
            signingConfig signingConfigs.debug
        }
    }

    flavorDimensions "flavors"

    productFlavors {
        prod {
            dimension "flavors"
            applicationId "cz.loono.app"
            resValue "string", "app_name", "loono"
        }
        dev {
            dimension "flavors"
            applicationId "cz.loono.app.dev"
            resValue "string", "app_name", "[dev] loono"
        }
    }
}

flutter {
    source '../..'
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.multidex:multidex:2.0.1'

}

apply plugin: 'com.google.firebase.crashlytics'
